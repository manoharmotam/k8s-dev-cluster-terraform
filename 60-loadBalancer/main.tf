resource "aws_lb" "frontend-lb" {
  name               = "frontend-alb"
  internal           = false
  load_balancer_type = "application"
  security_groups    = [local.loadBalancer_sg_id]
  subnets            = local.subnet_ids

  enable_deletion_protection = false

  tags = merge(
    local.common_tags,
    {
      Name = "${local.common_name}-loadBalancer"
    }
  )
}

resource "aws_lb_listener" "https" {
  load_balancer_arn = local.frontend_lb_arn
  port              = "443"
  protocol          = "HTTPS"
  ssl_policy        = "ELBSecurityPolicy-2016-08"
  certificate_arn   = local.certificate_arn

  default_action {
    type = "fixed-response"

    fixed_response {
      content_type = "text/html"
      message_body = "<h1>Welcome! This page is created by terraform</h1>"
      status_code  = "200"
    }
  }
}

resource "aws_route53_record" "www" {
  zone_id = var.zone_id
  name    = "*.${var.project}-${var.environment}.${local.domain_name}"
  type    = "A"

  alias {
    name                   = aws_lb.frontend-lb.dns_name
    zone_id                = aws_lb.frontend-lb.zone_id
    evaluate_target_health = true
  }

  allow_overwrite = true
}

resource "aws_lb_target_group" "app1" {
  name = "${local.common_name}-app1"
  port = 80
  protocol = "HTTP"
  vpc_id = local.vpc_id
  deregistration_delay = 30
  target_type = "ip"

  health_check {
    healthy_threshold = 2
    interval = 10
    matcher = "200-299"
    path = "/"
    port = 80
    protocol = "HTTP"
    timeout = 5
    unhealthy_threshold = 2
  }
}

resource "aws_lb_listener_rule" "app1" {
  listener_arn = aws_lb_listener.https.arn
  priority = 10

  action {
    type = "forward"
    target_group_arn = aws_lb_target_group.app1.arn
  }

  condition {
    host_header {
      values = ["app1-${var.environment}.${var.domain_name}"]
    }
  }
}